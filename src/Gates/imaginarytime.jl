### imaginarytime.jl
##
# A file for imaginary Pauli rotation gates, i.e., gates that are generated by a Pauli string.
# For example ImaginaryPauliRotation(:X, 2)  ->  exp(-τ/2 X_2) for imaginary time τ.
##
###

"""
A type for an imaginary Pauli rotation gate carrying the gate generator and the qubit indices on which it acts.
"""
struct ImaginaryPauliRotation <: ParametrizedGate
    symbols::Vector{Symbol}
    qinds::Vector{Int}

    @doc """
        ImaginaryPauliRotation(symbols::Vector{Symbol}, qinds::Vector{Int})
        ImaginaryPauliRotation(symbol::Symbol, qind::Int)

    A parametrized imaginary Pauli rotation generated by the Pauli string `symbols` acting on the qubits `qinds`.
    For example ImaginaryPauliRotation(:X, 2) or ImaginaryPauliRotation([:X, :Y], [1, 2]).
    We follow the convention that those rotation are equivalent by conjugation of 
    ```math
    e^{-θ/2 P}
    ```
    where `P` is the corresponding Pauli operator.
    """
    function ImaginaryPauliRotation(symbols, qinds)

        # turn symbols into vectors
        if isa(symbols, Symbol)
            symbols = [symbols]
        else
            symbols = vec(collect(symbols))
        end

        # check that the symbols are valid Pauli symbols
        _paulisymbolcheck(symbols)

        # turn qinds into vectors
        qinds = vec(collect(qinds))
        # check that the qubit indices are positive integers
        _qinds_check(qinds)

        # check that the number of symbols matches the number of qubits
        _qindslengthcheck(symbols, qinds)

        return new(symbols, qinds)
    end
end


"""
    ImaginaryPauliRotation(symbols, qinds, tau)

Constructor for a frozen `ImaginaryPauliRotation` generated by the Pauli string `symbols` acting on the qubits `qinds`,
and with fixed parameter `tau`.
"""
function ImaginaryPauliRotation(symbols, qinds, tau)
    return FrozenGate(ImaginaryPauliRotation(symbols, qinds), tau)
end
