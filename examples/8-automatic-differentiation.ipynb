{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "15cbaf9a-60d8-4998-97c2-fb013581470d",
   "metadata": {},
   "source": [
    "### An example of Automatic Differentiation"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2419cc7e-8c74-4019-9f9f-7dd76534e8f4",
   "metadata": {},
   "source": [
    "Pauli propagation has promise in optimizing variational models because its elementary operations are very simple and automatically differentiable. Additionally, it may be the case that strongly truncated simulations still yield final parameters that translate to high-quality quantum circuits on, for example, a quantum computer. This means that we could (perhaps) very cheaply train/optimize, and then deploy the learned model on a different classical or quantum method. There are some initial indications of this being possible, but they require further exploration. \n",
    "\n",
    "In this notebook, we demonstrate how to calculate fast automatic gradients of a quantum circuit using Pauli propagation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "20669a1a-a02c-43d9-ab41-f83aafd83ace",
   "metadata": {},
   "outputs": [],
   "source": [
    "using PauliPropagation"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "57585029-6b3d-40c4-871f-28900a04193f",
   "metadata": {},
   "source": [
    "Let us set up a simulation:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "707c3ac9-d46b-4c55-9607-2fcff94de271",
   "metadata": {},
   "outputs": [],
   "source": [
    "nq = 64\n",
    "\n",
    "topology = bricklayertopology(nq);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2a24c7c3-e326-4563-91dd-ce9044ee8765",
   "metadata": {},
   "source": [
    "We define a transverse field Hamiltonian, whose derivative we will compute. This could be used within a variational energy minimization routine to find its ground state. \n",
    "\n",
    "The Hamiltonian here reads $H = \\sum_{i}X_i + \\sum_{\\langle i, j\\rangle}Z_iZ_j$ where $ \\langle i, j\\rangle$ denotes neighbors on the topology."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "9e1d5fb1-5cc0-419f-a50a-5126a5c20e48",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "PauliSum(nqubits: 64, 127 Pauli terms:\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IZZIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIXIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIXIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIXIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIXIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIXIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       " 1.0 * IIIIIIIIIIIIIIIIIIII...\n",
       "  ⋮)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "H = PauliSum(nq)\n",
    "\n",
    "for qind in 1:nq\n",
    "    add!(H, :X, qind, 1.0)\n",
    "end\n",
    "\n",
    "for pair in topology\n",
    "    add!(H, [:Z, :Z], collect(pair), 1.0)\n",
    "end\n",
    "\n",
    "H"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c5f5e402-e941-4f2f-8f3e-2d0707c91e5d",
   "metadata": {},
   "source": [
    "Define some generic quantum circuit"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "12381cf7-77ff-481a-9098-518b71d34217",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "508"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "nl = 4\n",
    "\n",
    "# if we begin with the ZZ layer, it commutes with the |0> state and gives zero gradients\n",
    "circuit = tfitrottercircuit(nq, nl; topology=topology, start_with_ZZ=false)\n",
    "nparams = countparameters(circuit)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2ed752b6-0b10-4d0a-9a24-e0129edf1961",
   "metadata": {},
   "source": [
    "Importantly, we need to set our truncations. Depending on which package and which method you are using to compute your gradients, you can use different truncations. \n",
    "\n",
    "`ReverseDiff.jl` for example is a sophisticated package for automatic _reverse-mode_ differentiation (others may or may not work better). It will build a computational graph that it then differentiates using the chain rule. This is how large-scale neural networks are trained, and is commonly referred to as gradient _backpropagation_. The challenge here is that the fastest gradients are achieved with a _compiled_ version of the gradient function where the graph for the chain rule is computed once (to the best of our knowledge). In that case, only truncations during the initial computation will be respected. Truncations that we think work well here are `max_weight`, `max_freq`, and `max_sins`, as they do not depend on the particular parameters of the quantum circuit. On the other hand, which paths are explore with truncations such as `min_abs_coeff` will not be updated (again, to the best of our knowledge) as the gradients are computed. If you want to use such parameter-dependent truncations, you may need to use un-compiled gradient functions.\n",
    "\n",
    "`Mooncake.jl` is an up-and-coming package written fully in Julia that differentiates _the code itself_. It allows for reverse-mode differentiation with truncations like `min_abs_coeff` working and being differentiated as intended. We hope to provide a tutorial for this library soon.\n",
    "\n",
    "`ForwardDiff.jl` is a straight-up improvement over manual numerical differentiation, both in terms of speed and accuracy. Compared to reverse-mode differentiation libraries it is slower for circuits with more than several dozen parameters, but it requires very little additional memory. We will use it for this notebook."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0f55eeed-8972-4be0-9585-49733d70f138",
   "metadata": {},
   "source": [
    "Generate some generic parameters:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "36c63006-5d77-43ec-af6c-64b3a7e24212",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "Random.seed!(42)\n",
    "thetas = randn(nparams);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "285d3caa-30c5-431f-b3fd-0ebf2548477c",
   "metadata": {},
   "source": [
    "One expectation evaluation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "0bef1ff0-7fd3-4b78-8f54-14ee1f660140",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  1.175363 seconds (3.14 M allocations: 155.848 MiB, 1.91% gc time, 98.03% compilation time)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "7.26631983654383"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "min_abs_coeff = 1e-4\n",
    "max_weight = 5\n",
    "\n",
    "@time psum = propagate(circuit, H, thetas; min_abs_coeff, max_weight);\n",
    "overlapwithzero(psum)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "08b54e25-e5cb-4a8d-ac26-f02d849b2249",
   "metadata": {},
   "source": [
    "**Note**: To make these following functionsfaster, you should look into `let` blocks for local variable namespaces. You will want to define _closures_ and refrain from using global variables."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fec61b35-cf4c-459f-9cdc-d0ab48689011",
   "metadata": {},
   "source": [
    "#### What does not work:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2be8e1dc-c677-413e-a1c1-c34ba143ab58",
   "metadata": {},
   "source": [
    "Now wrap these steps into a function that takes only `thetas` as argument. (keep in mind our comment about using `let` blocks)\n",
    "\n",
    "This loss function does not work because the `ForwardDiff` package needs to propagate its custom coefficient type. But `H` is already strictly typed. So the following loss function would not be automatically differentiable."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "ff42aa41-fed2-42a3-974e-6c28861bcb5a",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "naivelossfunction (generic function with 1 method)"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function naivelossfunction(thetas)\n",
    "    # this function is not fully type-stable because min_abs_coeff and max_weight are global variables\n",
    "    psum = propagate(circuit, H, thetas; min_abs_coeff, max_weight);\n",
    "    return overlapwithzero(psum)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "21bfcf6c-e749-4421-a78e-01dcddd75af6",
   "metadata": {},
   "source": [
    "The loss works"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "f111331a-7405-4191-811f-cd5d26d88b57",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  0.239058 seconds (1.60 M allocations: 79.556 MiB, 4.53% gc time, 90.42% compilation time)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "7.26631983654383"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@time naivelossfunction(thetas)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "15507e9d-8eea-4a71-8547-daf0b931afbb",
   "metadata": {},
   "source": [
    "But the gradient would break with a cryptic error message."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "53158f29-cd6e-441b-a8ea-314ee633bf29",
   "metadata": {},
   "outputs": [],
   "source": [
    "# using Pkg; Pkg.add(\"ForwardDiff\")\n",
    "using ForwardDiff: gradient\n",
    "\n",
    "## this errors \n",
    "# gradient(naivelossfunction, thetas)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "34de0990-8fc3-4921-b45a-fc858bd0778b",
   "metadata": {},
   "source": [
    "#### What works:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5658ef0c-20ea-4bce-9e26-147a92a61bfc",
   "metadata": {},
   "source": [
    "To create a loss function that indeed works, we need the propagating Hamiltonian to have the correct coefficient type within the loss function. What tends to work is to type the coefficients in the Pauli sum to the element type of `thetas`, but this will not always work. In this case, it will make everything differentiable.\n",
    "\n",
    "Then we need to use the `propagate!()` function because otherwise the ForwardDiff library errors or yields zero gradients because we copy the Pauli sum while that library is accumulating the derrivative. Annoying... we agree! We will keep an eye out for better options.\n",
    "\n",
    "For maximal performance, we will want to make all functions type stable, which can be achieved by a function _closure_ or function _factory_: A function that outputs a function. In this case, we want to output a loss function that has all arguments other than `thetas` pre-loaded."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "df906060-d2e3-495f-b043-667da74181ea",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "generate_lossfunction (generic function with 1 method)"
      ]
     },
     "execution_count": 10,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function generate_lossfunction(circuit, H, min_abs_coeff, max_weight)\n",
    "    \n",
    "        function lossfunction(thetas)\n",
    "        # differentiation libraries use custom types to trace through the computation\n",
    "        # we need to make all of our objects typed like that so that nothing breaks\n",
    "        CoeffType = eltype(thetas)\n",
    "\n",
    "        # convert H to use the correct coefficient type\n",
    "        # otherwise it would error like above\n",
    "        grad_ready_H = convertcoefftype(CoeffType, H)\n",
    "\n",
    "        # be also need to run the in-place version with `!`, because by default we copy the Pauli sum\n",
    "        output_H = propagate!(circuit, grad_ready_H, thetas; min_abs_coeff, max_weight);\n",
    "        return overlapwithzero(output_H)\n",
    "    end\n",
    "\n",
    "    return lossfunction\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "bb39981a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# define the loss function that works with AD\n",
    "lossfunction = generate_lossfunction(circuit, H, min_abs_coeff, max_weight);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "666ca6a0-cc70-41f4-afb4-976a33813f68",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  0.144430 seconds (133.96 k allocations: 6.749 MiB, 84.14% compilation time)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "7.26631983654383"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@time lossfunction(thetas)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b310aca8-77fe-4a8e-9b8b-7022e1350e5c",
   "metadata": {},
   "source": [
    "And gradients work."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "3e17cbe3-e60e-48dc-86db-8c6dec773d86",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  2.875666 seconds (4.64 M allocations: 254.721 MiB, 1.16% gc time, 53.55% compilation time)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "508-element Vector{Float64}:\n",
       " -0.6772329930789089\n",
       " -0.22543783632035608\n",
       " -0.42292925319813446\n",
       " -0.4243076836337615\n",
       " -0.7523379732310989\n",
       " -0.07792066987114322\n",
       " -0.8515536079185417\n",
       "  0.01990979942877011\n",
       "  0.8974187316400088\n",
       " -0.25922854058847183\n",
       "  0.506894173618708\n",
       " -0.30613329539066253\n",
       " -0.8354345999607842\n",
       "  ⋮\n",
       "  1.0546803431129719\n",
       " -0.22261031482081436\n",
       "  0.4973379719992734\n",
       "  0.8133558792992438\n",
       "  0.24969214893578057\n",
       "  0.6969899813204755\n",
       "  0.17190365988833814\n",
       "  0.6511464180672394\n",
       "  0.03797818611225978\n",
       "  0.3563370395924771\n",
       " -0.21643500323976758\n",
       "  0.046996169676417704"
      ]
     },
     "execution_count": 13,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# the gradients of the first ZZ layer is \n",
    "@time gradient(lossfunction, thetas)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7f25da1f",
   "metadata": {},
   "source": [
    "You are now ready to go! Plug this gradient function into your favorite optimizer and see what happens.\n",
    "\n",
    "Remember: \n",
    "- These gradients are approximate when using truncation.\n",
    "- Forward-mode differentiation scales linearly with the number of parameters in addition to the extra runtime of simulating a deeper circuit.\n",
    "- Reverse-mode differentiation can be faster, and drastically so when the gradient is compiled at the cost of large memory overheads.\n",
    "- You can also propagate each Pauli term in the Hamiltonian individually and add up their gradients. That can be easily multi-threaded. "
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.2",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
